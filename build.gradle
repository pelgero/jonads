plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

version '0.1.1'
group 'dev.pelgero'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.12"
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
    }
}

check.dependsOn jacocoTestReport

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "pelgero"
            name "Robert Pelger"
            email "pelgero@gmail.com"
        }
    }

    scm {
        url "https://github.com/pelgero/jonads"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Implementation of functional types (monads) in Java')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/pelgero/jonads')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/pelgero/jonads.git'
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }

}


